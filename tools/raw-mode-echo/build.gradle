import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget
import org.jetbrains.kotlin.gradle.plugin.mpp.NativeBuildType

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply from: "$rootDir/addAllTargets.gradle"

kotlin {
	sourceSets {
		commonMain {
			dependencies {
				implementation projects.mosaicTerminal
				implementation libs.clikt
				implementation libs.finalizationHook
			}
		}
	}

	jvm {
		binaries {
			executable {
				mainClass = 'example.Main'
			}
		}
	}

	targets.withType(KotlinNativeTarget).configureEach { target ->
		target.binaries.executable {
			entryPoint = 'example.main'
			if (buildType == NativeBuildType.DEBUG) {
				linkTaskProvider.configure {
					enabled = false
				}
			}
		}
	}
}
