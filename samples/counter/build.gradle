import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget
import org.jetbrains.kotlin.gradle.targets.jvm.KotlinJvmTarget
import org.jetbrains.kotlin.gradle.plugin.mpp.NativeBuildType

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply from: "$rootDir/addAllTargets.gradle"

kotlin {
	sourceSets {
		commonMain {
			dependencies {
				implementation projects.mosaicRuntime
			}
		}
		commonTest {
			dependencies {
				implementation projects.mosaicTesting
				implementation libs.kotlin.test
				implementation libs.kotlinx.coroutines.test
				implementation libs.assertk
			}
		}
	}

	jvm {
		binaries {
			executable {
				mainClass = 'example.Main'
			}
		}
	}

	targets.withType(KotlinNativeTarget).configureEach { target ->
		target.binaries.executable {
			entryPoint = 'example.main'
			if (buildType == NativeBuildType.DEBUG) {
				linkTaskProvider.configure {
					enabled = false
				}
			}
		}
	}
}
