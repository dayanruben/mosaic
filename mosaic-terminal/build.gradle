apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply from: "$rootDir/addAllTargets.gradle"
apply from: "$rootDir/publish.gradle"
apply plugin: 'com.jakewharton.cite'
apply plugin: 'dev.drewhamilton.poko'

kotlin {
	explicitApi()

	sourceSets {
		configureEach {
			languageSettings {
				optIn('kotlin.contracts.ExperimentalContracts')
			}
			if (it.name.endsWith("Test")) {
				languageSettings {
					optIn('com.jakewharton.mosaic.terminal.TestApi')
					optIn('kotlin.ExperimentalStdlibApi')
				}
			}
		}

		commonMain {
			dependencies {
				api libs.kotlinx.coroutines.core
				implementation projects.mosaicTty
			}
		}
		commonTest {
			dependencies {
				implementation libs.kotlin.test
				implementation libs.kotlinx.coroutines.test
				implementation libs.assertk
			}
		}
	}

	tasks.withType(Test).configureEach {
		systemProperty('kotlinx.coroutines.test.default_timeout', '2s')
	}

	compilerOptions.freeCompilerArgs.add('-Xnon-local-break-continue')
}
