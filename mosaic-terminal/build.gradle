import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget

import static co.touchlab.cklib.gradle.CompileToBitcode.Language.C

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply from: "$rootDir/addAllTargets.gradle"
apply from: "$rootDir/publish.gradle"
apply plugin: 'co.touchlab.cklib'
apply plugin: 'dev.drewhamilton.poko'

kotlin {
	explicitApi()

	jvm {
		tasks.named(compilations.main.processResourcesTaskName).configure {
			doFirst {
				def files = project.fileTree('src/jvmMain/resources').files
				if (files.size() != 6) {
					throw new RuntimeException(
						"Missing native libraries. Run `zig build -p src/jvmMain/resources/jni`. Found: $files",
					)
				}
			}
		}
	}

	sourceSets {
		commonTest {
			dependencies {
				implementation libs.kotlin.test
				implementation libs.kotlinx.io
				implementation libs.kotlinx.coroutines.core
				implementation libs.assertk
			}
		}
		jvmTest {
			dependencies {
				implementation libs.kotlin.test.junit
			}
		}
	}

	targets.withType(KotlinNativeTarget).configureEach {
		compilations.main.cinterops {
			create('mosaic') {
				header(file('src/c/mosaic.h'))
				packageName('com.jakewharton.mosaic.terminal')
			}
		}
	}

	compilerOptions.freeCompilerArgs.add('-Xexpect-actual-classes')
	compilerOptions.freeCompilerArgs.add('-Xnon-local-break-continue')
}

cklib {
	config.kotlinVersion = libs.versions.kotlin.get()
	create('mosaic', file('src/c'), ['main']) {
		it.srcDirs = files('src/c')
		it.language = C
	}
}
